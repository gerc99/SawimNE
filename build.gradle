buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.2'
    }
}

apply plugin: 'android'
task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

repositories {
    maven { url "http://dl.bintray.com/populov/maven" }
    mavenCentral()
}

def mtmProject = project(':deps:mtm')

dependencies {
    compile mtmProject
    compile 'com.viewpagerindicator:library:2.4.1'
    compile 'com.android.support:support-v4:21.+'
    compile 'com.android.support:appcompat-v7:21.+'
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 52
        versionName '1.3.6'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    // signing config
    if (file("keystore.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("keystore.properties")))

        signingConfigs {
            sawim {
                storeFile file(props["key.store"])
                storePassword props["key.store.password"]
                keyAlias props["key.alias"]
                keyPassword props["key.alias.password"]
            }
        }

        buildTypes {
            release {
                runProguard true
                proguardFile getDefaultProguardFile('proguard-android.txt')
                proguardFile file('proguard-android.txt')
                signingConfig signingConfigs.sawim
            }
        }
    }
    android.applicationVariants.all { variant ->
        println "*********" + variant.description + "**********";
        def apkName = "SawimNE.apk";
        println "*********" + "$project.buildDir/apk/" + apkName + "**********";
        variant.outputFile = file("$project.buildDir/apk/" + apkName)
    }
}